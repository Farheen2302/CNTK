load=LocalMacros
run=DNN

LocalMacros = [
    ImageW = 32
    ImageH = 32
    ImageC = 3
    LabelDim = 10

    features = ImageInput(ImageW, ImageH, ImageC, tag = feature)
    featOffs = Const(128, rows = 3072)
    featScaled = Minus(features, featOffs)
    labels = Input(LabelDim, tag = label)
    
    convWScale = 7.07
    convBValue = 0
    fc1WScale = 12
    fc1BValue = 0
    
    scValue = 0.03
    
    kW = 3
    kH = 3
    
    hStride1 = 1
    vStride1 = 1
    hStride2 = 2
    vStride2 = 2
]

DNN=[
    cMap1 = 16
    conv1 = ConvBNReLULayer2(featScaled, cMap1, 27, kW, kH, hStride1, vStride1, convWScale, convBValue, scValue)

    rn1_1 = ResNetNode2(conv1, cMap1, 144, kW, kH, convWScale, convBValue, scValue)
    rn1_2 = ResNetNode2(rn1_1, cMap1, 144, kW, kH, convWScale, convBValue, scValue)
    rn1_3 = ResNetNode2(rn1_2, cMap1, 144, kW, kH, convWScale, convBValue, scValue)

    cMap2 = 32
    rn2_1_Wproj = Parameter(cMap2, cMap1, init = fromFile, initFromFilePath = "$Proj16to32Filename$", needGradient = false)
    rn2_1 = ResNetNode2Conv(rn1_3, cMap2, 144, 288, kW, kH, convWScale, convBValue, scValue, rn2_1_Wproj)
    rn2_2 = ResNetNode2(rn2_1, cMap2, 288, kW, kH, convWScale, convBValue, scValue)
    rn2_3 = ResNetNode2(rn2_2, cMap2, 288, kW, kH, convWScale, convBValue, scValue)

    cMap3 = 64
    rn3_1_Wproj = Parameter(cMap3, cMap2, init = fromFile, initFromFilePath = "$Proj32to64Filename$", needGradient = false)
    rn3_1 = ResNetNode2Conv(rn2_3, cMap3, 288, 576, kW, kH, convWScale, convBValue, scValue, rn3_1_Wproj)
    rn3_2 = ResNetNode2(rn3_1, cMap3, 576, kW, kH, convWScale, convBValue, scValue)
    rn3_3 = ResNetNode2(rn3_2, cMap3, 576, kW, kH, convWScale, convBValue, scValue)
                
    # pool
    poolW = 3
    poolH = 3
    poolhStride = 2
    poolvStride = 2
    pool = AveragePooling(rn3_3, poolW, poolH, poolhStride, poolvStride)

    ol = DnnLastLayer(576, labelDim, pool, fc1WScale, fc1BValue)
    
    CE = CrossEntropyWithSoftmax(labels, ol, tag = Criteria)
    Err = ErrorPrediction(labels, ol, tag = Eval)
    OutputNodes = ol
]

